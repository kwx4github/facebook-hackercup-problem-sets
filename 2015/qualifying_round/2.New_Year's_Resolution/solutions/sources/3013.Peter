/* Facebook Hackercup 2015 New Year's Resolution */
import io.
import util.
import cp.

main =>
	Outfile = open("newyearout.txt", write),
	read_file_lines("newyear.txt") = Lines,
	T = to_integer(Lines[1]),
	Idx = 2,
	foreach(TI in 1..T)
		G = split(Lines[Idx]), 
		[GP, GC, GF] = map(to_integer,G),
		Idx := Idx +1,
		N = to_integer(Lines[Idx]),
		P = new_array(N), C = new_array(N), F = new_array(N),
		foreach(I in 1..N)
			A = split(Lines[Idx+I]), 
			[P[I], C[I], F[I]] = map(to_integer,A)
		end,
		Res = cond(solvecase(GP,GC,GF,P,C,F,N), "yes", "no"),
		printf(Outfile,"Case #%d: %s%n", TI, Res),
	    Idx := Idx + N + 1
	end,
	close(Outfile).
	
solvecase(GP,GC,GF,P,C,F,N) =>
		S = new_array(N),
		S :: [0, 1],
		sum([P[I] * S[I] : I in 1..N]) #= GP,
		sum([C[I] * S[I] : I in 1..N]) #= GC,
		sum([F[I] * S[I] : I in 1..N]) #= GF,
		solve(S).