% B-Prolog 8.1 - http://www.probp.com/
% Usage:
% sed 's/^ */[/; s/ *$/]./; s/ \+/, /g' in-file | bp -g "cl('CookingTheBooks.pl'),main,halt" -l | tail -n +5 > out-file

swap_digit(A, B) :-
    append(Begin, [X | Middle], [Y | End], A),
    append(Begin, [Y | Middle], [X | End], B),
    \+ nth1(1, B, 0'0).

:- table smallest(+, min).
smallest(N, NewN) :-
    ( NewN = N ; swap_digit(N, NewN) ).
:- table largest(+, max).
largest(N, NewN) :-
    ( NewN = N ; swap_digit(N, NewN) ).

do_case(Case_num, N) :-
    number_codes(N, Nstr),
    smallest(Nstr, Smallest),
    largest(Nstr, Largest),
    format("Case #~w: ~s ~s\n", [Case_num, Smallest, Largest]).

main :-
    read([T]),
    foreach(Case_num in 1..T, [N], (
            read([N]),
            do_case(Case_num, N)
        )).
