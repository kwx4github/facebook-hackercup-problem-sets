// This program is written in Amber, a programming language that I personally
// designed and for which I'm currently implementing a compiler.
// You can find the compiler binaries, and all the other support files required to build
// this program on github at the following URL: https://github.com/gianantonio71/amber-build
// The commit that was specifically created to build this program is the one
// named "Facebook Hacker Cup 2015 - Qualification round". More recent ones will probably
// also works, but the language is a work in progress and I'm unable to guarantee
// backward compatibility.
// Just clone that repository and follow the instructions in the file README.md
// Email me at gianantonio71@gmail.com if you encounter any problems

solve(digits)
{
  len = length(digits);

  min_digits = digits;
  max_digits = digits;

  for (d1 @ i1 : digits)
    for (d2 @ i2 : digits)
      if (i1 < i2)
        if (i1 /= 0 or d2 /= ascii_0)
          new_digits = [if i == i1 then d2, i == i2 then d1 else d end : d @ i <- digits];
          min_digits = new_digits if is_lower(new_digits, min_digits);
          max_digits = new_digits if is_lower(max_digits, new_digits);
        ;
      ;
    ;
  ;

  return string(min_digits) & " " & string(max_digits);

  is_lower(s1, s2)
  {
    for (x1, x2 : zip(s1, s2))
      return x1 < x2 if x1 /= x2;
    ;
    return false;
  }
}


Main([String] args)
{
  if (length(args) /= 2)
    Print("Usage: <program name> <input file> <output file>\n");
    return;
  ;

  read_res = FileRead(args[0]);
  if (read_res == nil)
    Print("File not found: " & args[0] & "\n");
    return;
  ;
  input = value(read_res);

  lines = [string(l) : l <- split_lines(input)];

  test_cases = to_int(lines[0]);

  lc = 1;
  for (i_t = 0..test_cases-1)
    // Reading the inputs
    num = lines[lc];
    lc = lc + 1;

    // Solving the test case
    res_str = solve(_obj_(num));

    // Writing the results
    out_line = "Case #" & to_str(i_t + 1) & ": " & res_str & "\n";
    FileWrite(args[1], i_t > 0, _obj_(out_line));
    Print(out_line);
  ;
}