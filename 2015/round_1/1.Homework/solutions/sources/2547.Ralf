'Ralf Mouthaan
'Facebook Hacker Cup - Round 1 Question 1
'Homework

Option Explicit On
Option Strict On

Module Module1

    Private MaxValue As Integer = CInt(10 ^ 7)
    Private Primacity() As Integer

    Sub Main()

        Call Erastothenes()

        Dim DesktopPath As String = Environment.GetFolderPath(Environment.SpecialFolder.Desktop)
        Dim dlg As New System.Windows.Forms.OpenFileDialog
        dlg.InitialDirectory = DesktopPath
        dlg.Filter = "Text File|*.txt"
        dlg.ShowDialog()

        Dim reader As New System.IO.StreamReader(dlg.FileName)
        Dim writer As New System.IO.StreamWriter(DesktopPath & "\FHC_R1_Q1_Output.txt")

        Dim NoCases As Integer = CInt(reader.ReadLine)
        Dim SplitText() As String
        Dim Count As Integer
        For i = 1 To NoCases
            Count = 0
            SplitText = Split(reader.ReadLine, " ")
            For j = CInt(SplitText(0)) To CInt(SplitText(1))
                If Primacity(j) = CInt(SplitText(2)) Then
                    Count = Count + 1
                End If
            Next
            writer.Write("Case #" & CStr(i) & ": " & CStr(Count) & vbLf)
        Next

        writer.Close()
        reader.Close()

        System.Diagnostics.Process.Start("Wordpad.exe", DesktopPath & "\FHC_R1_Q1_Output.txt")

    End Sub

    Private Sub Erastothenes()

        'Actual a slight rehash of the sieve of erastothenes to count the number of primes a number is divisible by.

        Dim j As Integer
        ReDim Primacity(MaxValue)

        For i = 2 To MaxValue
            If Primacity(i) = 0 Then
                j = 1
                Primacity(i) += 1
                While (j + 1) * i <= MaxValue
                    j = j + 1
                    Primacity(i * j) += 1
                End While
            End If
        Next

    End Sub

End Module
